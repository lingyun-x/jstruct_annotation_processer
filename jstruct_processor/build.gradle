apply plugin: 'java-library'
apply plugin: 'kotlin'
apply plugin: 'kotlin-kapt'
apply plugin: 'maven-publish'
apply plugin: 'signing'

group 'com.lingyun-x'
version '0.2.5'

dependencies {
    implementation(files("${System.getProperty("java.home")}/../lib/tools.jar"))
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    implementation 'com.lingyun-x:jstruct_annotation:0.2.4'

    implementation 'com.squareup:kotlinpoet:1.7.2'
    kapt "com.google.auto.service:auto-service:1.0-rc7"
    implementation "com.google.auto.service:auto-service:1.0-rc7"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

java {
    withSourcesJar()
    withJavadocJar()
}

publishing {
    repositories {
        maven {
            name = "central"
            url = uri(nexusUrl)
            credentials {
                username = project.findProperty("nexusUsername") ?: System.getenv("NEXUS_USERNAME")
                password = project.findProperty("nexusPassword") ?: System.getenv("NEXUS_PASSWORD")
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = "jstruct_annotation_processor"
                url = 'https://github.com/lingyun-x/jstruct_annotation_processor'
                description = 'Interpret bytes as packed binary data'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'lingyun'
                        name = 'luolingfeng'
                        email = '463027714@qq.com'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/lingyun-x/jstruct_annotation_processor.git'
                    developerConnection = 'scm:git:ssh://github.com/lingyun-x/jstruct_annotation_processor.git'
                    url = 'https://github.com/lingyun-x/jstruct_annotation_processor/'
                }
            }
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.mavenJava
}
